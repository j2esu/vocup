apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.sdk_version

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
                arg("room.expandProjection", "true")
            }
        }

        // import api keys
        def localProps = new Properties()
        localProps.load(new FileInputStream(rootProject.file("local.properties")))
        buildConfigField("String", "PREDICTOR_API_KEY", "\"" + localProps['predictor.key'] + "\"")
        buildConfigField("String", "DICTIONARY_API_KEY", "\"" + localProps['dictionary.key'] + "\"")
        buildConfigField("String", "DICTIONARY_API_REGION", "\"" + localProps['dictionary.region'] + "\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    kotlinOptions {
        freeCompilerArgs += rootProject.ext.kotlin_args
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation "junit:junit:${rootProject.ext.junit_version}"
    androidTestImplementation "androidx.test.ext:junit:${rootProject.ext.android_junit_version}"

    implementation project(path: ':data_api')
    implementation project(path: ':base_util')

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    def room_version = '2.3.0'
    implementation "androidx.room:room-runtime:${room_version}"
    kapt "androidx.room:room-compiler:${room_version}"
    implementation "androidx.room:room-ktx:${room_version}"
}
